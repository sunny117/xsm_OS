alias functionNum R1;
alias returnValue R0;

//Buffered read
//The disk page provided is loaded to the corresponding buffer. The word present at the offset position of the buffer is copied to the Memory address provided as input. Buffer management is handled by this function internally.
if(functionNum == 2) then
	alias diskBlock R2;
	alias offset R3;
	alias memAddr R4;

	alias bufferNum R5;
	bufferNum = diskBlock%4;

	multipush(R1,R2,R3,R4,R5);
	R1 = 1;
	R2 = bufferNum;
	R3 = [SYSTEM_STATUS_TABLE + 1];
	call MOD_0;
	multipop(R1,R2,R3,R4,R5);

	if([BUFFER_TABLE + 4*bufferNum] != diskBlock) then
		if([BUFFER_TABLE + 4*bufferNum + 1]==1) then
			multipush(R1,R2,R3,R4,R5);
			R1 = 1;
			R2 = [SYSTEM_STATUS_TABLE + 1];
			R3 = 71 + bufferNum;
			R4 = [BUFFER_TABLE + 4*bufferNum];
			call MOD_4;
			multipop(R1,R2,R3,R4,R5);

			[BUFFER_TABLE + 4*bufferNum + 1] = 0;
		endif;

		multipush(R1,R2,R3,R4,R5);
		R1 = 2;
		R3 = 71 + bufferNum;
		R4 = diskBlock;
		R2 = [SYSTEM_STATUS_TABLE + 1];
		call MOD_4;
		multipop(R1,R2,R3,R4,R5);

		[BUFFER_TABLE + 4*bufferNum] = diskBlock;
	endif;

	[memAddr] = [(71 + bufferNum)*512 + offset];

	multipush(R1,R2,R3,R4,R5);
	R1 = 2;
	R2 = bufferNum;
	R3 = [SYSTEM_STATUS_TABLE + 1];
	call MOD_0;
	multipop(R1,R2,R3,R4,R5);

	return;
endif;

//Open
//Locates the file in the inode table and makes an entry in the Open File Table. Returns the Open File Table index or an error code if file does not exist or the table is full. On a successfull open, the file status table entry of the file is incremented.
if(functionNum == 3) then
	alias fileName R2;

	alias counter R3;
	counter = 0;

	while(counter < MAX_FILE_NUM) do
		if([INODE_TABLE + 16*counter + 1] == fileName) then
			break;
		endif;
		counter = counter + 1;
	endwhile;

	if(counter == MAX_FILE_NUM) then
		
		returnValue = -1;
		return;
	endif;

	alias inodeIndex R4;
	inodeIndex = counter;

	multipush(R1,R2,R3,R4);
	R1 = 4;
	R2 = inodeIndex;
	R3 = [SYSTEM_STATUS_TABLE + 1];
	call MOD_0;
	multipop(R1,R2,R3,R4);

	if(R0 == -1) then
		print "HI";
		returnValue = -1;
		return;
	endif;

	if([INODE_TABLE + 16*inodeIndex] == EXEC) then
		multipush(R1,R2,R3,R4);
		R1 = 5;
		R2 = inodeIndex;
		R3 = [SYSTEM_STATUS_TABLE + 1];
		call MOD_0;
		multipop(R1,R2,R3,R4);

		returnValue = -1;
		return;
	endif;

	counter = 0;
	while(counter<MAX_OPENFILE_NUM) do
		if([OPEN_FILE_TABLE + 4*counter] == -1) then
			break;
		endif;
		counter = counter + 1;
	endwhile;

	if(counter == MAX_OPENFILE_NUM) then
		multipush(R1,R2,R3,R4);
		R1 = 5;
		R2 = inodeIndex;
		R3 = [SYSTEM_STATUS_TABLE + 1];
		call MOD_0;
		multipop(R1,R2,R3,R4);
		
		returnValue = -2;
		return;
	endif;

	alias fileIndex R5;
	fileIndex = counter;

	if([INODE_TABLE + 16*inodeIndex + 1] == "root") then
		[OPEN_FILE_TABLE + 4*fileIndex] = INODE_ROOT;
	else
		[OPEN_FILE_TABLE + 4*fileIndex] = inodeIndex;

		if([FILE_STATUS_TABLE + 4*inodeIndex + 1] == -1) then
			[FILE_STATUS_TABLE + 4*inodeIndex + 1] = 1;
		else
			[FILE_STATUS_TABLE + 4*inodeIndex + 1] = [FILE_STATUS_TABLE + 4*inodeIndex + 1] + 1;
		endif;
	endif;

	[OPEN_FILE_TABLE + 4*fileIndex + 1] = 1;
	[OPEN_FILE_TABLE + 4*fileIndex + 2] = 0;

	multipush(R1,R2,R3,R4,R5);
	R1 = 5;
	R2 = inodeIndex;
	R3 = [SYSTEM_STATUS_TABLE + 1];
	call MOD_0;
	multipop(R1,R2,R3,R4,R5);
	
	returnValue = fileIndex;
	return;
endif;

//Close
//Closes the open instance of a file. Assumes a valid Opne File Table index is given as input.
if(functionNum == 4) then
	alias fileIndex R2;
	alias inodeIndex R3;

	inodeIndex = [OPEN_FILE_TABLE + 4*fileIndex];

	[OPEN_FILE_TABLE + 4*fileIndex + 1] = [OPEN_FILE_TABLE + 4*fileIndex + 1] - 1;

	if([OPEN_FILE_TABLE + 4*fileIndex + 1] == 0) then

		[OPEN_FILE_TABLE + 4*fileIndex + 0] = -1;
		[OPEN_FILE_TABLE + 4*fileIndex + 1] = -1;
		[OPEN_FILE_TABLE + 4*fileIndex + 2] = -1;
		[OPEN_FILE_TABLE + 4*fileIndex + 2] = -1;

		if([INODE_TABLE + 16*inodeIndex + 1] != "root") then
			[FILE_STATUS_TABLE + 4*inodeIndex + 1] = [FILE_STATUS_TABLE + 4*inodeIndex + 1] - 1;
		endif;

		if([FILE_STATUS_TABLE + 4*inodeIndex + 1] == 0) then
			[FILE_STATUS_TABLE + 4*inodeIndex + 1] = -1;
		endif;
	endif;
	return;
endif;
