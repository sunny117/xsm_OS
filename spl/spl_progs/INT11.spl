//Getpid, Getppid, Wait, Signal
alias userSP R0;
userSP = SP;

alias currentPID R1;
currentPID = [SYSTEM_STATUS_TABLE + 1];

[PROCESS_TABLE + currentPID * 16 + 13] = SP;
SP = [PROCESS_TABLE + currentPID * 16 + 11] * 512 - 1;

alias syscallNum R2;
syscallNum = [[PTBR + 2 * ((userSP - 5)/512)] * 512 + (userSP-5)%512];

[PROCESS_TABLE + currentPID * 16 + 9] = syscallNum;

//system call numbers for Getpid, Getppid, Wait and Signal are 11, 12,13 and 14 respectively.

//Getpid
if(syscallNum == 11) then
	[[PTBR + 2 * ((userSP - 1)/512)] * 512 + (userSP-1)%512] = currentPID;
endif;

//Getppid
if(syscallNum == 12) then
	[[PTBR + 2 * ((userSP - 1)/512)] * 512 + (userSP-1)%512] = [PROCESS_TABLE+currentPID*16+2];
endif;

//Wait
if(syscallNum == 13) then
	[PROCESS_TABLE + currentPID * 16 + 9] = 13;
	alias waitPID R2;
	waitPID = [[PTBR + 2 * ((userSP - 4)/512)] * 512 + (userSP-4)%512];
	if([PROCESS_TABLE+waitPID*16+4]==TERMINATED || waitPID == currentPID || waitPID >= 16 || waitPID < 0) then
		[[PTBR + 2*((userSP -1)/512)]*512 + (userSP-1)%512] = -1;
		[PROCESS_TABLE +currentPID*16 + 9] = 0;
		SP = [PROCESS_TABLE + currentPID*16 + 13];
		ireturn;
	endif;
	[PROCESS_TABLE + currentPID*16 + 4] = WAIT_PROCESS;
	[PROCESS_TABLE + currentPID*16 + 5] = waitPID;
	multipush(R0,R1,R2);
	call MOD_5;
	multipop(R0,R1,R2);
	[[PTBR + 2*((userSP -1)/512)]*512 + (userSP-1)%512] = 0;
	[PROCESS_TABLE +currentPID*16 + 9] = 0;
	SP = [PROCESS_TABLE + currentPID*16 + 13];
	ireturn;
endif;

//Signal
if(syscallNum == 14) then
	[PROCESS_TABLE + currentPID*16 + 9] = 14;
	alias count R2;
	count = 0;
	while(count < 16) do
		if([PROCESS_TABLE + count*16 + 4] == WAIT_PROCESS && [PROCESS_TABLE + count*16 + 5] == currentPID) then
			[PROCESS_TABLE + count*16 + 4] = READY;
		endif;
		count = count+1;
	endwhile;
	[[PTBR + 2*((userSP -1)/512)]*512 + (userSP-1)%512] = 0;
	[PROCESS_TABLE +currentPID*16 + 9] = 0;
	SP = [PROCESS_TABLE + currentPID*16 + 13];
	ireturn;
endif;

[PROCESS_TABLE +currentPID*16 + 9] = 0;
SP = [PROCESS_TABLE + [SYSTEM_STATUS_TABLE + 1]*16 + 13];
ireturn;
